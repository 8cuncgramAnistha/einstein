group 'io.github.asseco-pst'
version "1.1.0-SNAPSHOT"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath 'edu.sc.seis.gradle:launch4j:2.4.4'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

apply plugin: 'groovy'
apply plugin: 'signing'
apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'edu.sc.seis.launch4j'
apply plugin: 'com.bmuschko.nexus'

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-all
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.6', ext: 'pom'
    // https://mvnrepository.com/artifact/org.gitlab4j/gitlab4j-api
    compile group: 'org.gitlab4j', name: 'gitlab4j-api', version: '4.9.17'
    // https://mvnrepository.com/artifact/org.yaml/snakeyaml
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.24'
    // https://mvnrepository.com/artifact/com.vdurmont/semver4j
    compile group: 'com.vdurmont', name: 'semver4j', version: '2.2.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Main-Class': project.group + '.Main'
    }
}

shadowJar {
    baseName = "${rootProject.name}"
}

createExe {
    mainClassName = 'io.github.asseco.pst.Main'
    headerType = "console"
    icon = "${projectDir}/assets/einstein.ico"
    jar = "${rootDir}/build/libs/${rootProject.name}-${project.version}.jar"
    outfile = "${rootProject.name}-${project.version}.exe"
}

modifyPom {
    project {
        name 'einstein'
        description 'A runtime dependency manager'
        url 'https://github.com/asseco-pst/einstein'
        inceptionYear '2019'

        scm {
            url 'https://github.com/asseco-pst/einstein'
            connection 'scm:https://github.com/asseco-pst/einstein.git'
            developerConnection 'scm:git://github.com/asseco-pst/einstein.git'
        }

        developers {
            developer {
                id 'assecopst'
                name 'Asseco PST'
                email 'servicos.dcs@pst.asseco.com'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}